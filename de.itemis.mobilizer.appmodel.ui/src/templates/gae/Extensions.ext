import org::eclipse::emf::ecore;
import de::itemis::mobilizer::appModelDsl;

List[ViewCall] withoutConceptionalDuplicates(Object calls):
	JAVA templates.gae.Extensions.withoutConceptionalDuplicates(java.util.List);

List[ViewCall] uniqueViewCalls(Model this):
	viewCalls().toList().withoutConceptionalDuplicates();
	
uniqueViewCalls(Application this):
	uniqueViewCalls((Model)this.eContainer());

viewCalls(Model this):
	application.viewCalls().union(elements.viewCalls());
	
viewCalls(Application this):
	buttons.viewCalls().flatten();	

viewCalls(TabbarButton this):
	{view};
viewCalls(ModelElement this):
	{};
	
viewCalls(SectionedView this):
	this.sections.viewCalls().flatten();

viewCalls(ViewSection this):
	this.cells.viewCalls().flatten();
	
viewCalls(SectionCell this):
	ViewCall.isInstance(this.action)
		? {this.action}
		: {};

defProviderName(ViewCall this):
	this.provider.contentProvider().name;
	
defProviderParam(ViewCall this):
	defViewParam();	
		
defViewName(ViewCall this):
	this.view.name + this.provider.defViewNameSuffix(view);
	
defViewNameSuffix(ProviderConstruction this, View view):
	"By" + contentProvider().name.toFirstUpper();
	
defViewParam(ViewCall this):
	this.provider.contentProvider().defViewParam();	
	
defViewParam(ContentProvider this):
	parameter != null
		? parameter.name
		: "";

contentProvider(ProviderConstruction this):
	SimpleProviderConstruction.isInstance(this)
		? ((SimpleProviderConstruction)this).resolver()
		: ((ComplexProviderConstruction)this).provider;
		
ContentProvider resolver(SimpleProviderConstruction this):
	JAVA de.itemis.mobilizer.validation.AppModelDslJavaValidator.findResolver(de.itemis.mobilizer.appModelDsl.SimpleProviderConstruction);				
	
urlPattern(ViewCall this):
	let cp = this.provider.contentProvider() :
		'^' + this.view.name + "/" + cp.name + (
			cp.parameter == null ? "" : "/(?P<" + cp.parameter.name + ">.+?)"
		) + "/$";
		
viewTemplateFilename(ViewCall this):
	this.view.name + ".html";