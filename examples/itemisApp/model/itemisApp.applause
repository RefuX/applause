tabbarApplication itemisApp {

	
	button {
		title="News"
		icon="calendar.png"
		view=EventList( CurrentTimeline() )
	}

	button {
		title= "Blog"
		icon= "chat.png"
		view= BlogList(Blogposts())
	} 

	button { 
		title= "itemis"
		icon= "itemis.png"  
		view= OfficeList(CompanyDescription())
	}

	button {
		title= "Carreer"
		icon= "trophy.png"
		view= CareerView(CareerData())
	} 
 
}

/*
 * DESCRIBING DATA
 */

type String mapsTo "NSString"

const SANITIZE_SHEET "http://heikobehrens.net/misc/jazoon11/sanitize.php?url="

entity AllEvents {
	Event[] news
	Event[] activity
	Event[] workshop
}

entity Event {
	String id
	String kind
	String title
	String date
	String location
	String description
	String contact
	String link
	Contact[] speakers
}

entity Office {
	String id
	String location
	String summary
	String pictureurl
	Contact contact
	String phone
	String mail
	String address
	String geo
}

entity Contact {
	String name
	String role
	String bio
	String pictureurl
	String mail
	String phone
	WebAddress[] webaddresses
}

entity WebAddress {
	String title
	String url
}

entity Company {
	String description
	Office[] office
}

entity Career {
	String description
	Link[] link
	JobOffer[] job
}

entity Link {
	String title
	String url
}

entity JobOffer {
	String id
	String location
	String title
	String description
	String contact
}


contentprovider CompanyDescription
	returns Company
	fetches XML from "http://www.itemis.de/language=de/~xml.company/37606"
	selects "data.company"

contentprovider OfficeById(String id)
	returns Office
	fetches XML from ("http://www.itemis.de/language=de/~xml.applause/" id)
	selects "offices.office"
	
contentprovider CareerData
	returns Career
	fetches XML from "http://www.itemis.de/language=de/~xml.carreer/37606"
	selects "data.carreer"
	
contentprovider JobById(String id)
	returns JobOffer
	fetches XML from ("http://www.itemis.de/language=de/~xml.applause/" id)
	selects "jobs.job" 

tableview EventList(AllEvents allEvents) {
	title= "News"
	section {
		title= "News"
		cell Subtitle foreach allEvents.news as n {
			text= n.title
			details= n.date
			action= EventDetails(n)
		}
		}
	section {
		title= "Events"	
		cell Subtitle foreach allEvents.activity as a {
			text= a.title
			details= a.date
			action= EventDetails(a)
		}
	} 
	section {
		title= "Workshops"	
		cell Subtitle foreach allEvents.workshop as w {
			text= w.title
			details= w.date
			action= EventDetails(w)
		}
	} 
}

detailsview EventDetails(Event event) {
	title= "Event"
	header {
		title= event.title
		subtitle= (event.date " " event.location)
		details= event.description
	}
	
	section {
		cell Subtitle {
			text= event.contact
			details= "Contact"
			action= PersonDetails(PersonByName(event.contact))
		}
	}
	
	section {
		cell Subtitle foreach event.speakers as s {
			text= s.name
			details= s.role
			action= PersonDetails(PersonByName(s.name))
		}
	}
}

detailsview OfficeList(Company company) {
	title= "Company"
	header {  
		title= "itemis"
		subtitle= company.description
	}
	section {
		title= "Offices"
		cell Default foreach company.office as o {
			text= o.location
			action= OfficeDetails(OfficeById(o.id))
		}
	} 
}
 
detailsview OfficeDetails(Office office) {
	title= "Office"
	header {
		title= office.location
		//subtitle= office.address
		details= office.summary
	}
	
	section {
		cell Subtitle {
			text=  office.contact.name
			details= office.contact.role
			action= PersonDetails( PersonByName(office.contact.name) )
		} 
	}

	section {
		cell Map {
			text= office.address
			query= office.geo
			action= ("http://maps.google.de/maps?q=" urlconform(office.geo) )
		} 
		  
		cell Value2 {
			text= "phone"
			details= office.phone
			action= ("tel:" office.phone)
		}
		
		cell Value2 {
			text= "mail"
			details= office.mail
			action= ("mailto:" office.mail)
		}
	}

}


contentprovider CurrentTimeline
	returns AllEvents
	fetches XML from "http://www.itemis.de/language=de/~xml.timeline/37606"
	selects "data.events"
	
contentprovider EventById(Event e)
	resolves Event
	fetches XML from ("http://www.itemis.de/language=de/~xml.applause/" e.id)
	selects "events.event"	
	
contentprovider PersonByName(String name)
	returns Contact
	fetches XML from ("http://www.itemis.de/applause/people/de/" urlconform(name) ".xml")
	selects "contacts.contact"
	

entity BlogItem {
	String guid
	String title
	String link
	String description
	String creator
}

entity Person {
	String id
	String name
	String bio 
	String pictureurl
}

entity Speaker {
	String id
	String name
	String bio 
	String pictureurl
	Session[] sessions
}

entity Session {
	String id
	String title
	String description
	String date
	String timeslot
	String room
	Speaker[] speakers
}

/*
 * FETCHING DATA
 */

const FEEDSANITIZER_URL "http://feedsanitizer.appspot.com"

contentprovider Blogposts
	returns BlogItem[]
	fetches XML
		from (FEEDSANITIZER_URL "/sanitize?url=http%3A%2F%2Fblogs.itemis.de%2F%3Fshowfeed%3D1&format=rss")
		selects "rss.channel.item"
		
contentprovider BlogItemById(BlogItem b)
	resolves BlogItem
	fetches XML
		from (FEEDSANITIZER_URL "/sanitize?url=http%3A%2F%2Fblogs.itemis.de%2F%3Fshowfeed%3D1&format=rss&id=" urlconform(b.guid) )
		selects "rss.channel.item"
		
/*
 * DESCRIBING VIEWS
 */		

tableview BlogList(BlogItem[] items) {
	title= "Blog"
	section {
		cell Subtitle foreach items as item {
			text= item.creator
			details= item.title
			action= BlogDetails(item)
		}			
			
	}
}  

detailsview BlogDetails(BlogItem item) {
	title= "Post" 
	header {
		title= item.title
		details= item.description
	}
	section {
		cell Default {
			text= item.creator
			action= PersonDetails(PersonByName(item.creator))
		}		
		cell Default {
			text= "Open in Browser"
			action= item.link
		}
	}
} 

//tableview NewView(Session session) {
//	title= "foo" 
//}

detailsview PersonDetails(Contact person) {
	title= "Person" 
	header {
		title= person.name
		details= person.bio
		image= person.pictureurl
	}
	
	
	section {
		cell Value2 {
			text= "mail"
			details= person.mail
			action= ("mailto:" person.mail)
		}
		cell Value2 {
			text= "phone"
			details= person.phone
			action= ("tel:" person.phone)
		}
	}
	
	section {
		cell Default foreach person.webaddresses as w{
			text= w.title
			action= w.url
		}
	}
	
}

//	button {
//		title= "News"
//		icon= "chat.png" 
//		view= BlogList(Blogposts())
//	}

//	button { 
//		title= "Blog"
//		icon= "chat.png"  
//		view= BlogList(Blogposts())
//	}

detailsview CareerView(Career career) {
	 title= "Carreer"
	 header {
	 	subtitle= career.description
	 }
	 
	 section {
	 	title= "Links"
	 	cell Default foreach career.link as l {
	 		text= l.title
	 		action= l.url
	 	}
	 }
	 
	 section {
	 	title= "Job Offers"
	 	cell Subtitle foreach career.job as j {
	 		text= j.location
	 		details= j.title
	 		action= JobOfferDetails(JobById(j.id))
	 	}
	 }
	 
}

detailsview JobOfferDetails(JobOffer job) {
	title= "Job Offer"
	header {
		title= job.title
		details= job.description
	}
	
	section {
		cell Subtitle {
			text= job.contact
			details= "Contact"
			action= PersonDetails(PersonByName(job.contact))
		}
	}
}
