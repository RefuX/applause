tabbarApplication itemisApp {
	
	button {
		title= "News"
		icon= "chat.png"
		view= BlogList(Blogposts())
	}

	button {    
		title= "Tuesday"
		icon= "chat.png"
		view= SessionList( SessionsByDay("0") )
	}

	button {    
		title= "Wednesday"
		icon= "calendar.png"
		view= SessionList( SessionsByDay("1") )
	}
	
	button {    
		title= "Thursday"
		icon= "calendar.png"
		view= SessionList( SessionsByDay("2") )
	}
	
	button {
		title= "Speakers"
		icon= "person.png"
		view= SpeakersList( AllSpeakers() )
	}
}

/*
 * DESCRIBING DATA
 */

type String mapsTo "NSString"

entity BlogItem {
	String title
	String link
	String summary
	String author
}

entity Speaker {
	String name
	String bio
	String pictureurl
	Session[] sessions
}

entity Session {
	String title
	String description
	String timeslot
	String room
	Speaker[] speakers
}

/*
 * FETCHING DATA
 */


contentprovider Blogposts
	returns BlogItem[]
	fetches XML
		//from "http://pipes.yahoo.com/pipes/pipe.run?_id=53f737212b63f949b416d583de9124f7&_render=rss"
		from "http://query.yahooapis.com/v1/public/yql?q=USE%20%22http%3A%2F%2FHeikoBehrens.net%2Fese2010-36.xml%22%20AS%20t%3B%20%20%0ASELECT%20*%20FROM%20t"
		selects "query.results.feed.entry"
	
contentprovider SessionsByDay(String day)
	returns Session[]
	fetches XML
		from ("http://ese2010.sites.djangoeurope.com/sessions/day/" day "/?format=xml")
		selects "sessions.session"

//contentprovider VortragByTitel(String titel)
//	returns Session
//	fetches XML
//		from ("http://spreadsheets.google.com/feeds/list/0Au3-oaNYhfPIdEpRQWxpZnJyX2JCNUdtT1Z4M1B4SkE/1/public/values?sq=vortragid%3D" urlconform(replace(titel, " ", "")))
//		selects "feed.entry"
		
contentprovider AllSpeakers
	returns Speaker[]
	fetches XML
		from "http://ese2010.sites.djangoeurope.com/speakers/?format=xml"
		selects "result.speakers.speaker"

contentprovider SpeakerByName(String name)
	returns Speaker
	fetches XML
		from ("http://ese2010.sites.djangoeurope.com/speakers/name/" urlconform(name) "/?format=xml")
		selects "result.speaker"
		
/*
 * DESCRIBING VIEWS
 */		

tableview BlogList(BlogItem[] items) {
	title= "Eclipse Planet"
	section {
		cell Subtitle foreach items as i {
			text= i.author
			details= i.title
			action= BlogDetails(i)
		}
	} 
}  

detailsview BlogDetails(BlogItem item) {
	title= "Post" 
	header {
		title= item.title
		details= item.summary
	}
	section {
		cell Default {
			text= item.author
			action= SpeakerDetails(SpeakerByName(item.author))
		}		
		cell Default {
			text= "Open in Browser"
			action= item.link
		}
	}
} 


tableview SessionList(Session[] items) {
	title= "Sessions"
	section {
		cell Subtitle foreach items as i {
			text= i.title
			details= i.timeslot
			action= SessionDetails( i )   
		} 
	}
}

detailsview SessionDetails(Session session) {
	title= "Session"
	header {
		title= session.title
		subtitle= session.timeslot
		details= session.description
	}
	
	section {
		cell DefaultWithDisclosure foreach session.speakers as s {
			text= s.name
			image= s.pictureurl
			action= SpeakerDetails(SpeakerByName(s.name))
		}
		
	}
	
}

tableview SpeakersList(Speaker[] speakers) {
	title= "Speakers"
	section {
		cell Default foreach speakers as s {
			text= s.name
			image= s.pictureurl
			action= SpeakerDetails(SpeakerByName(s.name))   
		}
	}
}

detailsview SpeakerDetails(Speaker speaker) {
	title= "Speaker"
	header {
		title= speaker.name
		details= speaker.bio
		image= speaker.pictureurl
	}
	
	
	section {
		cell Default foreach speaker.sessions as session{
			text= session.title
			action= SessionDetails(session)
		}
	}
	
	section {
		cell Value2 {
			text= "mail"
			details= "some address"
			//action= ("mailto:"speaker.email) 
		}
		cell Value2 {
			text= "blog"
			details= "blog's url"
			// action= ("http://"speaker.blog) 
		}
	}
}

//	button {
//		title= "News"
//		icon= "chat.png" 
//		view= BlogList(Blogposts())
//	}

